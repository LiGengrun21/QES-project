int(0..100000000)[] SUN_START = [68, 160, 251, 343, 435, 526, 100000000];
int(0..100000000)[] SUN_END = [126, 218, 310, 401, 493, 585, 100000000];
const int SUN_LEN = 6;
int(0..SUN_LEN + 1) sun_i = 0;

int(0..100000000)[] UHF_START = [55, 147, 241, 336, 431, 528, 100000000];
int(0..100000000)[] UHF_END = [59, 155, 251, 346, 440, 532, 100000000];
const int UHF_LEN = 6;
int(0..UHF_LEN + 1) uhf_i = 0;

int(0..100000000)[] L_BAND_3F2_START = [7, 102, 198, 295, 396, 497, 100000000];
int(0..100000000)[] L_BAND_3F2_END = [100, 195, 291, 388, 489, 590, 100000000];
const int L_BAND_3F2_LEN = 6;
int(0..L_BAND_3F2_LEN + 1) l_band_3f2_i = 0;

int(0..100000000)[] L_BAND_3F3_START = [58, 154, 251, 352, 453, 100000000];
int(0..100000000)[] L_BAND_3F3_END = [151, 246, 344, 445, 546, 100000000];
const int L_BAND_3F3_LEN = 5;
int(0..L_BAND_3F3_LEN + 1) l_band_3f3_i = 0;

int(0..100000000)[] X_BAND_KOUROU_START = [129, 222, 100000000];
int(0..100000000)[] X_BAND_KOUROU_END = [135, 232, 100000000];
const int X_BAND_KOUROU_LEN = 2;
int(0..X_BAND_KOUROU_LEN + 1) x_band_kourou_i = 0;

int(0..100000000)[] X_BAND_TOULOUSE_START = [49, 143, 239, 335, 431, 526, 100000000];
int(0..100000000)[] X_BAND_TOULOUSE_END = [57, 153, 248, 344, 441, 536, 100000000];
const int X_BAND_TOULOUSE_LEN = 6;
int(0..X_BAND_TOULOUSE_LEN + 1) x_band_toulouse_i = 0;


// Clocks
clock time, job_time;
int current_time = 0;
int last_time = 0;

// Battery
int load = 2989; // Background power consumption
const int BATTERY_CAPACITY = 149760000;  // mJ
const int BATTERY_LowerBound = (int) (BATTERY_CAPACITY * 0.5); // 0.4
int(0..BATTERY_CAPACITY) battery_now = (int)(BATTERY_CAPACITY * 0.6);

// Power consumption
bool solar_l_band = false;  // increases sun power by 400 when true
int SUN_CONSUMPTION = 5700  * 60; 
int SUN_CONSUMPTION_L = 6100  * 60;  
const int BACKGROUND_CONSUMPTION = 2989 * 60;
const int UHF_CONSUMPTION = 2630 * 60;
const int L_BAND_CONSUMPTION = 3863 * 60;
const int X_BAND_CONSUMPTION = 11945 * 60;
const int WARMUP_CONSUMPTION = 414 * 60;
const int SLWEING_CONSUMPTION = 414 * 60;

// Jobs
bool L_active = false;
bool X_active = false;
const int WARMUP = 20;
const int SLEWING = 10;
int jobs_skipped = 0;
int uhf_jobs_done = 0;
int l_3f2_jobs_done = 0;
int l_3f3_jobs_done = 0;
int x_toulou_jobs_done = 0;
int x_kourou_jobs_done = 0;
// dynamic cost based on jobs done

// Actions
action SkipUhf, Skip3F2, Skip3F3, SkipXT, SkipXK;
//action updateSun, updateUhf, update3F2, update3F3, updateXT, updateXK;

action L3F2_warm_up, L3F2_start, L3F2_slewback, L3F2_end;
action L3F3_warm_up, L3F3_start, L3F3_slewback, L3F3_end;
action XK_warm_up, XK_start, XK_slewback, XK_end;
action XT_warm_up, XT_start, XT_slewback, XT_end;
action UHF_start, UHF_end;
action sun_start, sun_end, sun_start_extra, sun_end_extra;

action update_battery1, update_battery2,update_battery3, update_battery4, update_battery5, update_battery6;

// Properties
const int COST_Low = 1000;
const int COST_SKIP = 80;
transient int cost = 0;

// Maximum time the model should run for
const int MAX_TIME = 360;

// Cost is used to assign 1x Lband 2x Xband
property EfficientRoute = Xmin(S(cost), time >= MAX_TIME);

//function int BatteryCharge()
// Cap battery at CAPACITY or reduce cattery to 0 when less than BATTERY_EMPTY
//= (battery_charge - last_load * 60) > BATTERY_EMPTY ? min((battery_charge - last_load * 60), BATTERY_CAPACITY) : 0;
    
const int DynCost = 30;  // MAX = DynCost * 3
function int DynamicCost_3F2()
= (l_3f2_jobs_done * 2 >= l_3f3_jobs_done * 2 ? DynCost : 0) + (l_3f2_jobs_done * 2 >= x_toulou_jobs_done ? DynCost : 0) + (l_3f2_jobs_done * 2 >= x_kourou_jobs_done ? DynCost : 0);

function int DynamicCost_3F3()
= (l_3f3_jobs_done * 2 >= l_3f2_jobs_done * 2 ? DynCost : 0) + (l_3f3_jobs_done * 2 >= x_toulou_jobs_done ? DynCost : 0) + (l_3f3_jobs_done * 2 >= x_kourou_jobs_done ? DynCost : 0);

function int DynamicCost_XT()
= (x_toulou_jobs_done >= l_3f2_jobs_done * 2 ? DynCost : 0) + (x_toulou_jobs_done >= l_3f3_jobs_done * 2 ? DynCost : 0) + (x_toulou_jobs_done >= x_kourou_jobs_done ? DynCost : 0) + (l_3f2_jobs_done == 0 ? 80 : 0) + (l_3f3_jobs_done == 0 ? 80 : 0) ;

function int DynamicCost_XK()
= (x_kourou_jobs_done >= l_3f2_jobs_done * 2 ? DynCost : 0) + (x_kourou_jobs_done >= l_3f3_jobs_done * 2 ? DynCost : 0) + (x_kourou_jobs_done >= x_toulou_jobs_done ? DynCost : 0)+ (l_3f2_jobs_done == 0 ? 80 : 0) + (l_3f3_jobs_done == 0 ? 80 : 0);

process LinearBatteryModel()
{
    do
    { 
        alt
        {   
            //Battery is not empty and not full
        :: when ((battery_now - load * (current_time - last_time)) > BATTERY_LowerBound && (battery_now - load * (current_time - last_time)) < BATTERY_CAPACITY)  
            alt
            {
            :: update_battery1                    {= battery_now -= load * (current_time - last_time) =}
            :: update_battery2                    {= battery_now -= load * (current_time - last_time) =}
            :: update_battery3                    {= battery_now -= load * (current_time - last_time) =}
            :: update_battery4                    {= battery_now -= load * (current_time - last_time) =}
            :: update_battery5                    {= battery_now -= load * (current_time - last_time) =}
            :: update_battery6                    {= battery_now -= load * (current_time - last_time) =}
            }
            

            //Battery is fully charged
        :: when ((battery_now - load * (current_time - last_time)) >= BATTERY_CAPACITY)
            alt
            {
                :: update_battery1                   {= battery_now = BATTERY_CAPACITY =}
                :: update_battery2                   {= battery_now = BATTERY_CAPACITY =}
                :: update_battery3                   {= battery_now = BATTERY_CAPACITY =}
                :: update_battery4                   {= battery_now = BATTERY_CAPACITY =}
                :: update_battery5                   {= battery_now = BATTERY_CAPACITY =}
                :: update_battery6                   {= battery_now = BATTERY_CAPACITY =}
            }
            
            //Battery reaches lower bound
        :: when ((battery_now - load * (current_time - last_time)) <= BATTERY_LowerBound)
            alt
            {
                :: update_battery1                   {= cost += COST_Low =}; stop
                :: update_battery2                   {= cost += COST_Low =}; stop
                :: update_battery3                   {= cost += COST_Low =}; stop
                :: update_battery4                   {= cost += COST_Low =}; stop
                :: update_battery5                   {= cost += COST_Low =}; stop
                :: update_battery6                   {= cost += COST_Low =}; stop
            }
            
        }
    }
}

process Sun()
{
    do
    {
        invariant(time <= SUN_START[sun_i]) alt
        {
            // start extra sun power when L is active
            ::when (time >= SUN_START[sun_i] && L_active) sun_start_extra {= last_time = current_time, current_time = SUN_START[sun_i] =};
                invariant(false) update_battery1 {= load -= SUN_CONSUMPTION_L =};
            // end extra sun power
                invariant(time <= SUN_END[sun_i]) when (time >= SUN_END[sun_i]) sun_end {= last_time = current_time, current_time = SUN_END[sun_i], sun_i += 1 =};
                invariant(false) update_battery1 {= load += SUN_CONSUMPTION_L =}
            
            // start normal sun power
            ::when (time >= SUN_START[sun_i] && !L_active) sun_start {= last_time = current_time, current_time = SUN_START[sun_i] =};
                invariant(false) update_battery1 {= load -= SUN_CONSUMPTION =};
            // end normal sun power
                invariant(time <= SUN_END[sun_i]) when (time >= SUN_END[sun_i]) sun_end {= last_time = current_time, current_time = SUN_END[sun_i], sun_i += 1, sun_i++ =}; 
                invariant(false) update_battery1 {= load += SUN_CONSUMPTION =}
        }
    }
}

process UHF_job()
{
    do
    {
        // UHF job starts
        invariant(time <= UHF_START[uhf_i])
            when (time >= UHF_START[uhf_i]) UHF_start {= last_time = current_time, current_time = UHF_START[uhf_i] =};
            invariant(false) update_battery2 {= load += UHF_CONSUMPTION =};
        
        // UHF job ends
            invariant(time <= UHF_END[uhf_i]) when (time >= UHF_END[uhf_i]) UHF_end {= last_time = current_time, current_time = UHF_END[uhf_i], uhf_i += 1, uhf_jobs_done++ =};
            invariant(false) update_battery2 {= load -= UHF_CONSUMPTION =}

    }
}


process L_Band_Inmarsat_3F2_job()
{
    invariant(time <= (L_BAND_3F2_START[l_band_3f2_i] - WARMUP)) when (time >= (L_BAND_3F2_START[l_band_3f2_i] - WARMUP)) alt
    {
        :: when (!L_active && !X_active)
            L3F2_warm_up {= last_time = current_time, current_time = (L_BAND_3F2_START[l_band_3f2_i] - WARMUP), job_time = 0 =};
            invariant(false) update_battery3 {= load += WARMUP_CONSUMPTION =};

            invariant(job_time <= WARMUP) when (job_time >= WARMUP) L3F2_start {= last_time = current_time, current_time += WARMUP, job_time = 0, L_active = true =};
            invariant(false) update_battery3 {= load += (L_BAND_CONSUMPTION - WARMUP_CONSUMPTION) =};

            invariant(time <= L_BAND_3F2_END[l_band_3f2_i]) when (time >= L_BAND_3F2_END[l_band_3f2_i]) L3F2_slewback {= last_time = current_time, current_time = L_BAND_3F2_END[l_band_3f2_i], job_time = 0 =};
            invariant(false) update_battery3 {= load -= (L_BAND_CONSUMPTION - SLWEING_CONSUMPTION) =};

            invariant(job_time <= SLEWING) when (job_time >= SLEWING) L3F2_end {= last_time = current_time, current_time += SLEWING, L_active = false, l_3f2_jobs_done++, l_band_3f2_i++, cost += DynamicCost_3F2() =};
            invariant(false) update_battery3 {= load -= SLWEING_CONSUMPTION =};
            L_Band_Inmarsat_3F2_job()

        :: Skip3F2 {= l_band_3f2_i++, jobs_skipped++, cost += COST_SKIP, L_active = false =};
            L_Band_Inmarsat_3F2_job()
        :: when (L_active) Skip3F2 {= l_band_3f2_i++, jobs_skipped++, cost += COST_SKIP, L_active=false =};
            L_Band_Inmarsat_3F2_job()
    }
}


process L_Band_Inmarsat_3F3_job()
{
    invariant(time <= (L_BAND_3F3_START[l_band_3f3_i] - WARMUP)) when (time >= (L_BAND_3F3_START[l_band_3f3_i] - WARMUP)) alt
    {
        :: when (!L_active && !X_active)
            L3F3_warm_up {= last_time = current_time, current_time = (L_BAND_3F3_START[l_band_3f3_i] - WARMUP), job_time = 0 =};
            invariant(false) update_battery4 {= load += WARMUP_CONSUMPTION =};

            invariant(job_time <= WARMUP) when (job_time >= WARMUP) L3F3_start {= last_time = current_time, current_time += WARMUP, job_time = 0, L_active = true =};
            invariant(false) update_battery4 {= load += (L_BAND_CONSUMPTION - WARMUP_CONSUMPTION) =};

            invariant(time <= L_BAND_3F3_END[l_band_3f3_i]) when (time >= L_BAND_3F3_END[l_band_3f3_i]) L3F3_slewback {= last_time = current_time, current_time = L_BAND_3F3_END[l_band_3f3_i], job_time = 0 =};
            invariant(false) update_battery4 {= load -= (L_BAND_CONSUMPTION - SLWEING_CONSUMPTION) =};

            invariant(job_time <= SLEWING) when (job_time >= SLEWING) L3F3_end {= last_time = current_time, current_time += SLEWING, L_active = false, l_3f3_jobs_done++, l_band_3f3_i++, cost += DynamicCost_3F3() =};
            invariant(false) update_battery4 {= load -= SLWEING_CONSUMPTION =};
            L_Band_Inmarsat_3F3_job()

        :: Skip3F3 {= l_band_3f3_i++, jobs_skipped++, cost += COST_SKIP, L_active = false =};
            L_Band_Inmarsat_3F3_job()
        :: when (L_active) Skip3F3 {= l_band_3f3_i++, jobs_skipped++, cost += COST_SKIP, L_active = false =};
            L_Band_Inmarsat_3F3_job()
    }
}


process X_Band_Kourou_job()
{
    invariant(time <= (X_BAND_KOUROU_START[x_band_kourou_i] - WARMUP)) when (time >= (X_BAND_KOUROU_START[x_band_kourou_i] - WARMUP)) alt
    {
        :: when (!X_active && !L_active)
            XK_warm_up {= last_time = current_time, current_time = (X_BAND_KOUROU_START[x_band_kourou_i] - WARMUP), job_time = 0 =};
            invariant(false) update_battery5 {= load += WARMUP_CONSUMPTION =};

            invariant(job_time <= WARMUP) when (job_time >= WARMUP) XK_start {= last_time = current_time, current_time += WARMUP, job_time = 0, X_active = true =};
            invariant(false) update_battery5 {= load += (X_BAND_CONSUMPTION - WARMUP_CONSUMPTION) =};

            invariant(time <= X_BAND_KOUROU_END[x_band_kourou_i]) when (time >= X_BAND_KOUROU_END[x_band_kourou_i]) XK_slewback {= last_time = current_time, current_time = X_BAND_KOUROU_END[x_band_kourou_i], job_time = 0 =};
            invariant(false) update_battery5 {= load -= (X_BAND_CONSUMPTION - SLWEING_CONSUMPTION) =};

            invariant(job_time <= SLEWING) when (job_time >= SLEWING) XK_end {= last_time = current_time, current_time += SLEWING, X_active = false, x_kourou_jobs_done++, x_band_kourou_i++, cost += DynamicCost_XK() =};
            invariant(false) update_battery5 {= load -= SLWEING_CONSUMPTION =};
            X_Band_Kourou_job()

        :: SkipXK {= x_band_kourou_i++, jobs_skipped++, cost += COST_SKIP, X_active=false =};
            X_Band_Kourou_job()
        :: when (X_active) SkipXK {= x_band_kourou_i++, jobs_skipped++, cost += COST_SKIP, X_active=false =};
            X_Band_Kourou_job()
    }
}

process X_Band_Toulouse_job()
{
    invariant(time <= (X_BAND_TOULOUSE_START[x_band_toulouse_i] - WARMUP)) when (time >= (X_BAND_TOULOUSE_START[x_band_toulouse_i] - WARMUP)) alt
    {
        :: when (!X_active && !L_active)
            XT_warm_up {= last_time = current_time, current_time = (X_BAND_TOULOUSE_START[x_band_toulouse_i] - WARMUP), job_time = 0 =};
            invariant(false) update_battery6 {= load += WARMUP_CONSUMPTION =};

            invariant(job_time <= WARMUP) when (job_time >= WARMUP) XT_start {= last_time = current_time, current_time += WARMUP, job_time = 0, X_active = true =};
            invariant(false) update_battery6 {= load += (X_BAND_CONSUMPTION - WARMUP_CONSUMPTION) =};

            invariant(time <= X_BAND_TOULOUSE_END[x_band_toulouse_i]) when (time >= X_BAND_TOULOUSE_END[x_band_toulouse_i]) XT_slewback {= last_time = current_time, current_time = X_BAND_TOULOUSE_END[x_band_toulouse_i], job_time = 0 =};
            invariant(false) update_battery6 {= load -= (X_BAND_CONSUMPTION - SLWEING_CONSUMPTION) =};

            invariant(job_time <= SLEWING) when (job_time >= SLEWING) XT_end {= last_time = current_time, current_time += SLEWING, X_active = false, x_toulou_jobs_done++, x_band_toulouse_i++, cost += DynamicCost_XT() =};
            invariant(false) update_battery6 {= load -= SLWEING_CONSUMPTION =};
            X_Band_Toulouse_job()

        :: SkipXK {= x_band_toulouse_i++, jobs_skipped++, cost += COST_SKIP, X_active=false =};
            X_Band_Toulouse_job()
        :: when (X_active) SkipXK {= x_band_toulouse_i++, jobs_skipped++, cost += COST_SKIP, X_active=false =};
            X_Band_Toulouse_job()
    }
}


par
{
    :: LinearBatteryModel()
    :: Sun()
    :: UHF_job()
    :: L_Band_Inmarsat_3F2_job()
    :: L_Band_Inmarsat_3F3_job()
    :: X_Band_Kourou_job()
    :: X_Band_Toulouse_job()
}
